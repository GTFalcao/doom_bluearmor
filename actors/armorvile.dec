actor Armorvile : Archvile replaces Archvile {
  // spawn pickable blue armors instead of fire
  // damage ignores armor
  health 900
  states {
    Heal:
      VILE "[\]" 5 bright A_SpawnItemEx("BlueArmorBonus", random(30, 40), random(-20, 20), 30)
      Goto See
    Pain:
		  VILE Q 2
      VILE QQQ 1 A_SpawnItemEx("BlueArmorBonus", 0, 0, 40, random(-3, 3), random(-3, 3), random(2, 5))
		  VILE Q 5 A_Pain
		  Goto See
    Missile:
      VILE G 0 Bright A_VileStart
      VILE G 10 Bright A_FaceTarget
      VILE H 8 Bright A_VileTarget
      VILE IJKLMN 8 Bright A_FaceTarget
      VILE O 8 Bright A_VileAttack("vile/stop", 20, 75, 75, 3.0)
      VILE P 20 Bright
      Goto See
    Death:
      VILE QQ 3 A_SpawnItemEx("BlueArmorBonus", 0, 0, 40, random(-3, 3), random(-3, 3), random(2, 5))
      VILE Q 1 A_SpawnItemEx("BlueArmorBonus", 0, 0, 40, random(-3, 3), random(-3, 3), random(2, 5))
      VILE R 2 A_Scream
      VILE R 3 A_SpawnItemEx("BlueArmorBonus", 0, 0, 40, random(-3, 3), random(-3, 3), random(2, 5))
      VILE R 2 A_SpawnItemEx("BlueArmorBonus", 0, 0, 40, random(-3, 3), random(-3, 3), random(2, 5))
      VILE S 1 A_NoBlocking
      VILE SS 3 A_SpawnItemEx("BlueArmorBonus", 0, 0, 32, random(-3, 3), random(-3, 3), random(2, 5))
      VILE TT 3 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
      VILE T 1 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
      VILE U 2
      VILE U 3 A_SpawnItemEx("BlueArmorBonus", 0, 0, 16, random(-3, 3), random(-3, 3), random(2, 5))
      VILE U 2 A_SpawnItemEx("BlueArmorBonus", 0, 0, 16, random(-3, 3), random(-3, 3), random(2, 5))
      VILE VWXY 7
      VILE Z -1 
      Stop
  }
}

ACTOR ArmorvileFire : ArchvileFire replaces ArchvileFire
{
  translation "160:167=200:207"
  States
  {
  Spawn:
    FIRE A 2 Bright A_StartFire
    FIRE BA 2 Bright A_Fire
    FIRE B 0 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
    FIRE B 2 Bright A_Fire
    FIRE C 2 Bright A_FireCrackle
    FIRE B 0 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
    FIRE BC 2 Bright A_Fire
    FIRE B 0 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
    FIRE BC 2 Bright A_Fire
    FIRE D 0 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
    FIRE DC 2 Bright A_Fire
    FIRE D 0 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
    FIRE DC 2 Bright A_Fire
    FIRE D 0 A_SpawnItemEx("BlueArmorBonus", 0, 0, 24, random(-3, 3), random(-3, 3), random(2, 5))
    FIRE DEDED 2 Bright A_Fire
    FIRE E 2 Bright A_FireCrackle
    FIRE FEFEFGHGHGH 2 Bright A_Fire
    Stop
  }
}

actor BlueArmorBonus : BasicArmorBonus
{
  Radius 12
  Height 10
  scale 0.6
  Inventory.PickupMessage "Picked up a mini blue armor."
  Inventory.Icon "ARM2A0"
  Armor.SavePercent 50
  Armor.SaveAmount 3
  States
  {
  Spawn:
    ARM2 A 5
    ARM2 B 5 Bright
    Loop
  }
}

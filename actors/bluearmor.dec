actor BlueArmorMonster : LostSoul replaces LostSoul {
	health 120
	mass 100
	radius 24
  height 32
  scale 1.5
	painchance 128
  damage (random(10, 20))
	obituary "%o was fucked up by a Blue Armor."
  bloodtype "BlueArmorChunkSmall"
  +BRIGHT
  -MISSILEMORE
	states
	{
		Spawn:
			ARM2 AB 12
      loop
		See:
			ARM2 AABB 3 A_Chase
			loop
		Missile:
			ARM2 A 10
      ARM2 B 3 A_SkullAttack
      ARM2 AB 3
			goto Missile+2
    Pain:
      ARM2 A 3
      ARM2 A 3 A_Pain
      goto See
		Death:
			ARM2 A 3
			ARM2 A 3 A_Scream
      TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("BlueArmorChunk", random(-7, 7), random(-7, 7), random(0, 20), randompick(-1, 1) * frandom(2.0, 6.0), randompick(-1, 1) * frandom(2.0, 6.0), frandom(2.0, 6.0))
			TNT1 A 1 A_Fall
			stop
	}
}

actor BlueArmorChunk {
  scale 0.25
  radius 3
  height 4
  bouncetype Doom
  PROJECTILE
  -NOGRAVITY
  -ACTIVATEPCROSS
  +BRIGHT
  +CANNOTPUSH
  +USEBOUNCESTATE
  +BOUNCEONACTORS
  states {
    Spawn:
      BRMR ABCDEFGH 1
      loop
    Bounce.Floor:
      "----" A 70 A_ChangeFlag("USEBOUNCESTATE", false)
      "----" A 1 A_FadeOut(0.05)
      wait
  }
}

actor BlueArmorChunkSmall : BlueArmorChunk {
  scale 0.125
  radius 2
  height 2
  -USEBOUNCESTATE
  states {
    Spawn:
      BRMR ABCDEFGHABCDEFGH 1
      BRMR ABCDEFGHA 1 A_FadeOut(0.1)
      stop
  }
}
